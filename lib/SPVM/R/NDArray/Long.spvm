# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::Long extends R::NDArray {
  
  # Fields
  method data : long[] () {
    
    return (long[])$self->{data};
  }
  
  # Class Mehtods
  static method new : R::NDArray::Long ($options : object[] = undef) {
    
    my $self = new R::NDArray::Long;
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  method create_default_data : long[] ($length : int = 0) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new long[$length];
    
    return $data;
  }
  
  method elem_to_string : string ($data : long[], $data_index : int) {
    
    my $string = (string)$data->[$data_index];
    
    return $string;
  }
  
  method elem_assign : void ($dist_data : long[], $dist_data_index : int, $src_data : long[], $src_data_index : int) {
    
    $dist_data->[$dist_data_index] = $src_data->[$src_data_index];
  }
  
  method elem_clone : void ($dist_data : long[], $dist_data_index : int, $src_data : long[], $src_data_index : int) {
    
    $dist_data->[$dist_data_index] = $src_data->[$src_data_index];
  }
  
  method elem_cmp : int ($a_data : long[], $a_data_index : int, $b_data : long[], $b_data_index : int) {
    
    my $cmp = $a_data->[$a_data_index] <=> $b_data->[$b_data_index];
    
    return $cmp;
  }
  
  method elem_is_na : int ($data : long[], $data_index : int) {
    
    my $elem_is_na = 0;
    
    return $elem_is_na;
  }
  
  method clone : R::NDArray::Long ($shallow : int = 0) {
    return (R::NDArray::Long)$self->SUPER::clone($shallow);
  }
  
  method slice : R::NDArray::Long ($indexes_product : R::NDArray::Int[]) {
    return (R::NDArray::Long)$self->SUPER::slice($indexes_product);
  }
  
  precompile method to_int_ndarray : R::NDArray::Int () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Int->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = (int)$data->[$i];
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_float_ndarray : R::NDArray::Float () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Float->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = (float)$data->[$i];
    }
    
    return $ret_ndarray;
  }
  
  precompile method to_double_ndarray : R::NDArray::Double () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::Double->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i] = (double)$data->[$i];
    }
    
    return $ret_ndarray;
  }
  
}

