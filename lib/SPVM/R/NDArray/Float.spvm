# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::Float extends R::NDArray {
  
  use Sort;
  
  # Fields
  method data : float[] () {
    
    return (float[])$self->{data};
  }
  
  # Class Mehtods
  static method new : R::NDArray::Float ($options : object[] = undef) {
    
    my $self = new R::NDArray::Float;
    
    $self->init($options);
    
    return $self;
  }
  
  protected method init : void ($options : object[] = undef) {
    
    $self->SUPER::init($options);
  }
  
  method create_default_data : float[] ($length : int = 0) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new float[$length];
    
    return $data;
  }
  
  method clone : R::NDArray::Float ($elem_shallow_clone : int = 0) {
    return (R::NDArray::Float)$self->SUPER::clone($elem_shallow_clone);
  }
  
  method elem_to_string : string ($data : float[], $data_index : int) {
    
    my $elem = $data->[$data_index];
    
    my $string = (string)(float)$elem;
    
    return $string;
  }
  
  method elem_clone : void ($dist_data : float[], $dist_data_index : int, $src_data : float[], $src_data_index : int) {
    
    $dist_data->[$dist_data_index] = $src_data->[$src_data_index];
  }
  
  precompile method set_order : void ($indexes_ndarray : R::NDArray::Int) {
    
    $self->check_set_order_args($indexes_ndarray);
    
    my $indexes = $indexes_ndarray->data;
    
    my $data = (float[])$self->{data};
    
    my $tmp = $self->clone;
    
    my $tmp_data = $tmp->data;
    
    my $length = $self->length;
    for (my $i = 0; $i < $length; $i++) {
      my $order_index = $indexes->[$i];
      
      $data->[$i] = $tmp_data->[$order_index];
    }
  }
  
  method slice_data : float[] ($data_indexes : int[]) {
    
    unless ($data_indexes) {
      die "The data indexes \$data_indexes must be defined.";
    }
    
    my $data_indexes_length = @$data_indexes;
    my $data = $self->data;
    my $ret_data = new float[$data_indexes_length];
    for (my $i = 0; $i < $data_indexes_length; $i++) {
      my $data_index = $data_indexes->[$i];
      $ret_data->[$i] = $data->[$data_index];
    }
    
    return $ret_data;
  }
  
  precompile method slice_set_data : void ($data_indexes : int[], $data : float[]) {
    
    my $self_data = $self->data;
    
    unless ($data_indexes) {
      die "The data indexes \$data_indexes must be defined.";
    }
    
    for (my $i = 0; $i < @$data; $i++) {
      my $data_index = $data_indexes->[$i];
      $self_data->[$data_index] = $data->[$i];
    }
  }
  
  method slice : R::NDArray::Float ($vector_indexes : R::NDArray::Int[]) {
    return (R::NDArray::Float)$self->SUPER::slice($vector_indexes);
  }
  
  method slice_set : void ($vector_indexes : R::NDArray::Int[], $ndarray : R::NDArray::Float) {
    $self->SUPER::slice_set($vector_indexes, $ndarray);
  }
  
  method elem_cmp : int ($data : float[], $a_data_index : int, $b_data_index : int) {
    
    my $cmp = $data->[$a_data_index] <=> $data->[$b_data_index];
    
    return $cmp;
  }
  
}
