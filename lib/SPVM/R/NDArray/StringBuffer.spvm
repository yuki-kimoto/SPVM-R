# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::StringBuffer extends R::NDArray {
  
  use Fn;
  use Array;
  use StringBuffer;
  
  # Class Mehtods
  static method new : R::NDArray::StringBuffer ($options : object[] = undef) {
    
    my $self = new R::NDArray::StringBuffer;
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  method data : StringBuffer[] () {
    
    return (StringBuffer[])$self->{data};
  }
  
  method create_default_data : StringBuffer[] ($length : int = 0) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new StringBuffer[$length];
    
    return $data;
  }
  
  method elem_to_string : string ($data : StringBuffer[], $data_index : int) {
    
    my $elem = $data->[$data_index];
    
    unless ($elem) {
      return "undef";
    }
    
    my $string = $elem->to_string;
    
    return $string;
  }
  
  method elem_assign : void ($dist_data : StringBuffer[], $dist_data_index : int, $src_data : StringBuffer[], $src_data_index : int) {
    
    $dist_data->[$dist_data_index] = $src_data->[$src_data_index];
  }
  
  method elem_clone : void ($dist_data : StringBuffer[], $dist_data_index : int, $src_data : StringBuffer[], $src_data_index : int) {
    
    my $src_elem = $src_data->[$src_data_index];
    
    if ($src_elem) {
      $dist_data->[$dist_data_index] = StringBuffer->new($src_elem->to_string);
    }
    else {
      $dist_data->[$dist_data_index] = undef;
    }
  }
  
  method elem_cmp : int ($data : StringBuffer[], $a_data_index : int, $b_data_index : int) {
    
    my $a_elem = $data->[$a_data_index];
    
    my $a_string = (string)undef;
    if ($a_elem) {
      $a_string = $a_elem->to_string;
    }
    
    my $b_elem = $data->[$b_data_index];
    my $b_string = (string)undef;
    if ($b_elem) {
      $b_string = $b_elem->to_string;
    }
    
    my $cmp = $a_string cmp $b_string;
    
    return $cmp;
  }
  
  method elem_is_na : int ($data : StringBuffer[], $data_index : int) {
    
    my $elem_is_na = 0;
    if (!$data->[$data_index]) {
      $elem_is_na = 1;
    }
    
    return $elem_is_na;
  }
  
  method clone : R::NDArray::StringBuffer ($shallow : int = 0) {
    return (R::NDArray::StringBuffer)$self->SUPER::clone($shallow);
  }
  
  method slice : R::NDArray::StringBuffer ($asix_indexes_product : R::NDArray::Int[]) {
    return (R::NDArray::StringBuffer)$self->SUPER::slice($asix_indexes_product);
  }
  
}
