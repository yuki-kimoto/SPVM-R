# Copyright (c) 2024 Yuki Kimoto
# MIT License

# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::Byte extends R::NDArray {
  
  use ByteList;
  
  # Class Mehtods
  static method new : R::NDArray::Byte ($options : object[] = undef) {
    
    my $self = new R::NDArray::Byte;
    
    $self->init($options);
    
    return $self;
  }
  
  protected method init : void ($options : object[] = undef) {
    
    $options = Fn->merge_options($options, {element_size => 1, type => "byte", is_numeric => 1});
    
    $self->SUPER::init($options);
  }
  
  method data : byte[] () {
    
    return (byte[])$self->{data};
  }
  
  protected method check : void () {
    
    my $data = (byte[])$self->{data};
    
    my $length = $self->length;
    
    unless (@$data == $length) {
      die "The length of data field must be the same as length field.";
    }
  }
  
  protected method create_default_data : byte[] ($length : int) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new byte[$length];
    
    return $data;
  }
  
  method clone : R::NDArray::Byte () {
    return (R::NDArray::Byte)$self->SUPER::clone;
  }
  
  method element_to_string : string ($data_index : int) {
    
    my $data = $self->data;
    
    my $element = $data->[$data_index];
    
    my $string = (string)(byte)$element;
    
    return $string;
  }
  
  precompile method set_order : void ($indexes_ndarray : R::NDArray::Int) {
    
    $self->check_set_order_args($indexes_ndarray);
    
    my $indexes = $indexes_ndarray->data;
    
    my $data = (byte[])$self->{data};
    
    my $tmp = $self->clone;
    
    my $tmp_data = $tmp->data;
    
    my $length = $self->length;
    for (my $i = 0; $i < $length; $i++) {
      my $order_index = $indexes->[$i];
      
      $data->[$i] = $tmp_data->[$order_index];
    }
  }
  
  protected method slice_data : object ($data_indexes : int[]) {
    
    unless ($data_indexes) {
      die "The data indexes \$data_indexes must be defined.";
    }
    
    my $list = ByteList->new;
    my $data_indexes_length = @$data_indexes;
    my $data = $self->data;
    for (my $i = 0; $i < $data_indexes_length; $i++) {
      my $element = $data->[$i];
      $list->push($element);
    }
    my $ret_data = $list->to_array;
    
    return $ret_data;
  }
  
  protected method slice_set_data : object ($data_indexes : int[], $data : object) {
    
  }
  
}
