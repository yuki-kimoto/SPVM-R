# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::DoubleComplex extends R::NDArray {
  
  use Complex_2d;
  
  # Class Mehtods
  static method new : R::NDArray::DoubleComplex ($options : object[] = undef) {
    
    my $self = new R::NDArray::DoubleComplex;
    
    $self->init($options);
    
    return $self;
  }
  
  protected method init : void ($options : object[] = undef) {
    
    $options = Fn->merge_options($options, {element_size => 16, type => "Complex_2d", is_numeric => 1});
    
    $self->SUPER::init($options);
  }
  
  method data : Complex_2d[] () {
    
    return (Complex_2d[])$self->{data};
  }
  
  protected method check : void () {
    
    my $data = (Complex_2d[])$self->{data};
    
    my $length = $self->length;
    
    unless (@$data == $length) {
      die "The length of data field must be the same as length field.";
    }
  }
  
  protected method create_default_data : Complex_2d[] ($length : int) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new Complex_2d[$length];
    
    return $data;
  }
  
  method clone : R::NDArray::DoubleComplex () {
    return (R::NDArray::DoubleComplex)$self->SUPER::clone;
  }
  
  method element_to_string : string ($data_index : int) {
    
    my $data = $self->data;
    
    my $element = $data->[$data_index];
    
    my $string = (string)(double)$element->{re} . " + " . (string)(double)$element->{im};
    
    return $string;
  }
  
  precompile method set_order : void ($indexes : int[]) {
    
    $self->check_set_order_args($indexes);
    
    my $data = (Complex_2d[])$self->{data};
    
    my $tmp = $self->clone;
    
    my $tmp_data = $tmp->data;
    
    my $length = $self->length;
    for (my $i = 0; $i < $length; $i++) {
      my $order_index = $indexes->[$i];
      
      $data->[$i] = $tmp_data->[$order_index];
    }
  }
  
  precompile method to_float_complex : R::NDArray::FloatComplex () {
    
    unless ($self) {
      die "The n-dimension array \$self must be defined.";
    }
    
    my $length = $self->length;
    
    my $x_dim = $self->dim;
    
    my $ret_ndarray = R::OP::FloatComplex->c(undef, $x_dim);
    
    my $x_data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i]{re} = (float)$x_data->[$i]{re};
      $ret_data->[$i]{im} = (float)$x_data->[$i]{im};
    }
    
    return $ret_ndarray;
  }
  
}

