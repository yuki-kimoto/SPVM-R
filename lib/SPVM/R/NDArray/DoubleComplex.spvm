# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::NDArray::DoubleComplex extends R::NDArray {
  
  use Complex_2d;
  
  # Fields
  method data : Complex_2d[] () {
    
    return (Complex_2d[])$self->{data};
  }
  
  # Class Mehtods
  static method new : R::NDArray::DoubleComplex ($options : object[] = undef) {
    
    my $self = new R::NDArray::DoubleComplex;
    
    $self->init($options);
    
    return $self;
  }
  
  # Instance Methods
  method create_default_data : Complex_2d[] ($length : int = 0) {
    
    unless ($length >= 0) {
      die "The length \$length must be more than or equal to 0.";
    }
    
    my $data = new Complex_2d[$length];
    
    return $data;
  }
  
  method clone : R::NDArray::DoubleComplex ($shallow : int = 0) {
    return (R::NDArray::DoubleComplex)$self->SUPER::clone($shallow);
  }
  
  method elem_to_string : string ($data : Complex_2d[], $data_index : int) {
    
    my $elem = $data->[$data_index];
    
    my $string = (string)(double)$elem->{re} . " + " . (string)(double)$elem->{im};
    
    return $string;
  }
  
  method elem_assign : void ($dist_data : Complex_2d[], $dist_data_index : int, $src_data : Complex_2d[], $src_data_index : int) {
    
    $dist_data->[$dist_data_index] = $src_data->[$src_data_index];
  }
  
  method elem_clone : void ($dist_data : Complex_2d[], $dist_data_index : int, $src_data : Complex_2d[], $src_data_index : int) {
    
    $dist_data->[$dist_data_index] = $src_data->[$src_data_index];
  }
  
  method slice : R::NDArray::DoubleComplex ($asix_indexes_product : R::NDArray::Int[]) {
    return (R::NDArray::DoubleComplex)$self->SUPER::slice($asix_indexes_product);
  }
  
  precompile method to_float_complex_ndarray : R::NDArray::FloatComplex () {
    
    my $length = $self->length;
    
    my $dim = $self->dim;
    
    my $ret_ndarray = R::OP::FloatComplex->c(undef, $dim);
    
    my $data = $self->data;
    my $ret_data = $ret_ndarray->data;
    
    for (my $i = 0; $i < $length; $i++) {
      $ret_data->[$i]{re} = (float)$data->[$i]{re};
      $ret_data->[$i]{im} = (float)$data->[$i]{im};
    }
    
    return $ret_ndarray;
  }
  
}

