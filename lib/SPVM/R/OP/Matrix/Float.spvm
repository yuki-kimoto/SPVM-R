# Copyright (c) 2024 Yuki Kimoto
# MIT License

class R::OP::Matrix::Float {
  
  use R::NDArray::Float;
  use R::OP::Float;
  use R::OP::Matrix;
  use Sys;
  use Hash;
  use R::NDArray::Hash;
  
  static method matrix : R::NDArray::Float ($data : object of Float|float[]|R::NDArray::Float, $nrow : int, $ncol : int) {
    
    my $ndarray = R::OP::Float->c($data, [$nrow, $ncol]);
    
    return $ndarray;
  }
  
  static method matrix_byrow : R::NDArray::Float ($data : object of Float|float[]|R::NDArray::Float, $nrow : int, $ncol : int) {
    
    my $ndarray = &matrix($data, $nrow, $ncol);
    
    $data = $ndarray->data;
    
    my $length = @(float[])$data;
    
    my $new_data = new float[$length];
    
    for (my $data_index = 0; $data_index < $length; $data_index++) {
      my $elem = $data->(float[])->[$data_index];
      
      my $new_row_index = $data_index / $ncol;
      
      my $new_column_index = $data_index - $ncol * $new_row_index;
      
      my $new_data_index = $nrow * $new_column_index + $new_row_index;
      
      $new_data->[$new_data_index] = $elem;
    }
    
    my $ret_ndarray = &matrix($new_data, $nrow, $ncol);
    
    return $ret_ndarray;
  }
  
  
  static method cbind : R::NDArray::Float ($x_ndarray : R::NDArray::Float, $y_ndarray : R::NDArray::Float) {
    return (R::NDArray::Float)R::OP::Matrix->cbind($x_ndarray, $y_ndarray);
  }
  
  static method rbind : R::NDArray::Float ($x_ndarray : R::NDArray::Float, $y_ndarray : R::NDArray::Float) {
    return (R::NDArray::Float)R::OP::Matrix->rbind($x_ndarray, $y_ndarray);
  }
  
  static method diag : R::NDArray::Float ($x_ndarray : R::NDArray::Float) {
    
    unless ($x_ndarray) {
      die "The n-dimensional array \$x_ndarray must be defined.";
    }
    
    unless ($x_ndarray->is_matrix) {
      die "The n-dimensional array \$x_ndarray must be a matrix.";
    }
    
    my $x_data = (float[])$x_ndarray->data;
    
    my $x_row = $x_ndarray->nrow;
    
    my $x_column = $x_ndarray->ncol;
    
    my $ret_dim = [$x_row, $x_column];
    
    my $ret_ndarray = R::OP::Float->c(undef, $ret_dim);
    
    my $ret_data = $ret_ndarray->data;
    
    my $x_index = 0;
    for (my $row = 0; $row < $x_row; $row++) {
      $ret_data->[$x_column * $row + $row] = $x_data->[$row];
    }
    
    return $ret_ndarray;
  }
  
  static method diag_identity : R::NDArray::Float ($nrow : int) {
    
    unless ($nrow > 0) {
      die "The row \$nrow must be greater than 0.";
    }
    
    my $ncol = $nrow;
    
    my $ret_dim = [$nrow, $ncol];
    
    my $ret_ndarray = R::OP::Float->c(undef, $ret_dim);
    
    my $ret_data = $ret_ndarray->data;
    
    for (my $row = 0; $row < $nrow; $row++) {
      $ret_data->[$ncol * $row + $row] = 1;
    }
    
    return $ret_ndarray;
  }
  
  static method mul : R::NDArray::Float ($x_ndarray : R::NDArray::Float, $y_ndarray : R::NDArray::Float) {
    
    unless ($x_ndarray) {
      die "The n-dimensional array \$x_ndarray must be defined.";
    }
    
    unless ($x_ndarray->is_matrix) {
      die "The n-dimensional array \$x_ndarray must be a matrix.";
    }
    
    unless ($y_ndarray) {
      die "The n-dimensional array \$y_ndarray must be defined.";
    }
    
    unless ($y_ndarray->is_matrix) {
      die "The n-dimensional array \$y_ndarray must be a matrix.";
    }
    
    my $x_data = (float[])$x_ndarray->data;
    
    my $x_row = $x_ndarray->nrow;
    
    my $x_column = $x_ndarray->ncol;
    
    my $y_data = (float[])$y_ndarray->data;
    
    my $y_row = $y_ndarray->nrow;
    
    my $y_column = $y_ndarray->ncol;
    
    unless ($x_column == $y_row) {
      die "The column of the matrix \$x_ndarray must be equal to the row of the matrix \$y_ndarray.";
    }
    
    my $ret_data_ref = new float[][1];
    
    my $ret_row = -1;
    
    my $ret_column = -1;
    
    &_mul($ret_data_ref, \$ret_row, \$ret_column, $x_data, $x_row, $x_column, $y_data, $y_row, $y_column);
    
    my $ret_dim = [$ret_row, $ret_column];
    
    my $ret_data = $ret_data_ref->[0];
    
    my $ret_ndarray = R::OP::Float->c($ret_data, $ret_dim);
    
    return $ret_ndarray;
  }
  
  static method t : R::NDArray::Float ($x_ndarray : R::NDArray::Float) {
    
    unless ($x_ndarray) {
      die "The n-dimensional array \$x_ndarray must be defined.";
    }
    
    unless ($x_ndarray->is_square_matrix) {
      die "The n-dimensional array \$x_ndarray must be a squre matrix.";
    }
    
    my $x_data = (float[])$x_ndarray->data;
    
    my $x_row = $x_ndarray->nrow;
    
    my $x_column = $x_ndarray->ncol;
    
    my $ret_data_ref = new float[][1];
    
    my $ret_row = -1;
    
    my $ret_column = -1;
    
    &_t($ret_data_ref, \$ret_row, \$ret_column, $x_data, $x_row, $x_column);
    
    my $ret_data = $ret_data_ref->[0];
    
    my $ret_dim = [$ret_row, $ret_column];
    
    my $ret_ndarray = R::OP::Float->c($ret_data, $ret_dim);
    
    return $ret_ndarray;
  }
  
  static method det : R::NDArray::Float ($x_ndarray : R::NDArray::Float) {
    
    unless ($x_ndarray) {
      die "The n-dimensional array \$x_ndarray must be defined.";
    }
    
    unless ($x_ndarray->is_square_matrix) {
      die "The n-dimensional array \$x_ndarray must be a squre matrix.";
    }
    
    my $x_data = (float[])$x_ndarray->data;
    
    my $x_row = $x_ndarray->nrow;
    
    my $x_column = $x_ndarray->ncol;
    
    my $ret_data_ref = new float[][1];
    
    my $ret_row = -1;
    
    my $ret_column = -1;
    
    &_det($ret_data_ref, \$ret_row, \$ret_column, $x_data, $x_row, $x_column);
    
    my $ret_data = $ret_data_ref->[0];
    
    my $ret_dim = [$ret_row, $ret_column];
    
    my $ret_ndarray = R::OP::Float->c($ret_data, $ret_dim);
    
    return $ret_ndarray;
  }
  
  static method solve : R::NDArray::Float ($x_ndarray : R::NDArray::Float) {
    
    unless ($x_ndarray) {
      die "The n-dimensional array \$x_ndarray must be defined.";
    }
    
    unless ($x_ndarray->is_square_matrix) {
      die "The n-dimensional array \$x_ndarray must be a squre matrix.";
    }
    
    my $x_data = (float[])$x_ndarray->data;
    
    my $x_row = $x_ndarray->nrow;
    
    my $x_column = $x_ndarray->ncol;
    
    my $ret_data_ref = new float[][1];
    
    my $ret_row = -1;
    
    my $ret_column = -1;
    
    &_solve($ret_data_ref, \$ret_row, \$ret_column, $x_data, $x_row, $x_column);
    
    my $ret_data = $ret_data_ref->[0];
    
    my $ret_dim = [$ret_row, $ret_column];
    
    my $ret_ndarray = R::OP::Float->c($ret_data, $ret_dim);
    
    return $ret_ndarray;
  }
  
  static method eigen : R::NDArray::Hash ($x_ndarray : R::NDArray::Float) {
    
    unless ($x_ndarray) {
      die "The n-dimensional array \$x_ndarray must be defined.";
    }
    
    unless ($x_ndarray->is_square_matrix) {
      die "The n-dimensional array \$x_ndarray must be a squre matrix.";
    }
    
    my $x_data = (float[])$x_ndarray->data;
    
    my $x_row = $x_ndarray->nrow;
    
    my $x_column = $x_ndarray->ncol;
    
    my $eigen_vectors_data_ref = new float[][1];
    
    my $eigen_values_data_ref = new float[][1];
    
    my $eigen_vectors_row = -1;
    
    my $eigen_vectors_column = -1;
    
    &_eigen($eigen_vectors_data_ref, \$eigen_vectors_row, \$eigen_vectors_column, $x_data, $x_row, $x_column, $eigen_values_data_ref);
    
    my $eigen_vectors_data = $eigen_vectors_data_ref->[0];
    
    my $eigen_vectors_dim = [$eigen_vectors_row, $eigen_vectors_column];
    
    my $eigen_vectors_ndarray = R::OP::Float->c($eigen_vectors_data, $eigen_vectors_dim);
    
    my $eigen_values_data = $eigen_values_data_ref->[0];
    
    my $eigen_values_ndarray = R::OP::Float->c($eigen_values_data);
    
    my $ret_ndarray_hash = R::NDArray::Hash->new;
    
    $ret_ndarray_hash->set("vectors", $eigen_vectors_ndarray);
    
    $ret_ndarray_hash->set("values", $eigen_values_ndarray);
    
    return $ret_ndarray_hash;
  }
  
  # Private Class Methods
  private native static method _mul : void ($ret_data_ref : float[][], $ret_row_ref : int*, $ret_column_ref : int*, $x_data : float[], $x_row :int, $x_column : int, $y_data : float[], $y_row : int, $y_column : int);
  
  private native static method _det : void ($ret_data_ref : float[][], $ret_row_ref : int*, $ret_column_ref : int*, $x_data : float[], $x_row : int, $x_column : int);
  
  private native static method _t : void ($ret_data_ref : float[][], $ret_row_ref : int*, $ret_column_ref : int*, $x_data : float[], $x_row : int, $x_column : int);
  
  private native static method _solve : void ($ret_data_ref : float[][], $ret_row_ref : int*, $ret_column_ref : int*, $x_data : float[], $x_row :int, $x_column : int);
  
  private native static method _eigen : void ($ret_data_ref : float[][], $ret_row_ref : int*, $ret_column_ref : int*, $x_data : float[], $x_row : int, $x_column : int, $eigen_values_data_ref : float[][]);
}
